S -> SentNorm  : SentNorm [quest=0]
S -> SentInv   : SentInv  [quest=1]
S -> SentCmd   : SentCmd

Neg -> : -mA 	[neg=1]
Neg -> : 	[neg=0]

SentCmd -> don't V 	: V -mA [neg=1]
SentCmd -> do Not V 	: V Neg
SentCmd -> V 		: V  	[neg=0]

# affirmative 
SentNorm -> Subj AuxBePos Not StBe      : Subj StBe Tense 
SentNorm -> Subj AuxBeCont Not StBe     : Subj StBe Tense
SentNorm -> Subj AuxBeNeg StBe          : Subj StBe Tense           	[neg=1]
SentNorm -> Subj AuxHavePos Not StHave  : Subj StHave Tense
SentNorm -> Subj AuxHaveCont Not StHave : Subj StHave Tense
SentNorm -> Subj AuxHaveNeg StHave      : Subj StHave Tense        	[neg=1]
SentNorm -> Subj AuxDoPos Not StDo      : Subj StDo Tense
SentNorm -> Subj AuxDoNeg StDo          : Subj StDo Tense          	[neg=1]
SentNorm -> Subj ModalPos Not StModal   : Subj StModal ModalTense
SentNorm -> Subj ModalCont Not StModal  : Subj StModal ModalTense
SentNorm -> Subj ModalNeg StModal       : Subj StModal ModalTense  	[neg=1]
SentNorm -> Subj Vs                     : Subj Vs SimpTense	   	[pass=0,neg=0,mode=simp,tense=pres,numb=sing,pers=3]
SentNorm -> Subj V                      : Subj V SimpTense         	[pass=0,neg=0,mode=simp,tense=pres,numb=sing,pers=1]
SentNorm -> Subj V                      : Subj V SimpTense         	[pass=0,neg=0,mode=simp,tense=pres,numb=plur]
SentNorm -> Subj Ved                    : Subj Ved SimpTense       	[pass=0,neg=0,mode=simp,tense=past]

# question
SentInv -> AuxBePos Subj Not StBe      : Subj StBe 
SentInv -> AuxBeNeg Subj StBe          : Subj StBe      [neg=1]
SentInv -> AuxHavePos Subj Not StHave  : Subj StHave 
SentInv -> AuxHaveNeg Subj StHave      : Subj StHave    [neg=1]
SentInv -> AuxDoPos Subj Not StDo      : Subj StDo 
SentInv -> AuxDoNeg Subj StDo          : Subj StDo      [neg=1]
SentInv -> ModalPos Subj Not StModal   : Subj StModal 
SentInv -> ModalNeg Subj StModal       : Subj StModal   [neg=1]

AuxBePos   -> am      [tense=pres,pers=1,numb=sing]
AuxBePos   -> are     [tense=pres,pers=2,numb=sing]
AuxBePos   -> is      [tense=pres,pers=3,numb=sing]
AuxBePos   -> are     [tense=pres,numb=plur]
AuxBePos   -> was     [tense=past,numb=sing,pers=1]
AuxBePos   -> were    [tense=past,numb=sing,pers=2]
AuxBePos   -> was     [tense=past,numb=sing,pers=3]
AuxBePos   -> were    [tense=past,numb=plur]
AuxBeCont  -> 'm      [tense=pres,pers=1,numb=sing]
AuxBeCont  -> 're     [tense=pres,pers=2,numb=sing]
AuxBeCont  -> 's      [tense=pres,pers=3,numb=sing]
AuxBeCont  -> 're     [tense=pres,numb=plur]
AuxBeNeg   -> isn't   [tense=pres,pers=3,numb=sing]
AuxBeNeg   -> aren't  [tense=pres,numb=plur]
AuxBeNeg   -> wasn't  [tense=past,numb=sing,pers=1]
AuxBeNeg   -> weren't [tense=past,numb=sing,pers=2]
AuxBeNeg   -> wasn't  [tense=past,numb=sing,pers=3]
AuxBeNeg   -> weren't [tense=past,numb=plur]
AuxHavePos -> have    [tense=pres,numb=sing,pers=1]
AuxHavePos -> have    [tense=pres,numb=sing,pers=2]
AuxHavePos -> has     [tense=pres,numb=sing,pers=3]
AuxHavePos -> have    [tense=pres,numb=plur]
AuxHavePos -> had     [tense=past]
AuxHaveCont -> 've    [tense=pres,numb=sing,pers=1]
AuxHaveCont -> 've    [tense=pres,numb=sing,pers=2]
AuxHaveCont -> 's     [tense=pres,numb=sing,pers=3]
AuxHaveCont -> 've    [tense=pres,numb=plur]
AuxHaveCont -> 'd     [tense=past]
AuxHaveNeg -> haven't [tense=pres,numb=sing,pers=1]
AuxHaveNeg -> haven't [tense=pres,numb=sing,pers=2]
AuxHaveNeg -> haven't [tense=pres,numb=plur]
AuxHaveNeg -> hasn't  [tense=pres,numb=sing,pers=3]
AuxHaveNeg -> hadn't  [tense=past]
AuxDoPos   -> do      [tense=pres,numb=sing,pers=1]
AuxDoPos   -> do      [tense=pres,numb=sing,pers=2]
AuxDoPos   -> do      [tense=pres,numb=plur]
AuxDoPos   -> does    [tense=pres,numb=sing,pers=3]
AuxDoPos   -> did     [tense=past]
AuxDoNeg   -> don't   [tense=pres,numb=sing,pers=1]
AuxDoNeg   -> don't   [tense=pres,numb=sing,pers=2]
AuxDoNeg   -> don't   [tense=pres,numb=plur]
AuxDoNeg   -> doesn't [tense=pres,numb=sing,pers=3]
AuxDoNeg   -> didn't  [tense=past]
ModalPos -> can       [modal=can,tense=pres]
ModalPos -> could     [modal=can,tense=past]
ModalPos -> will      [modal=will,tense=pres]
ModalPos -> shall     [modal=will,tense=pres]
ModalPos -> would     [modal=will,tense=past]
ModalPos -> must      [modal=must]
ModalPos -> may       [modal=can,tense=pres]
ModalPos -> might     [modal=can,tense=pres]
ModalPos -> should    [modal=must]
ModalCont -> 'll      [modal=will,tense=pres]
ModalCont -> 'd       [modal=will,tense=past]
ModalNeg -> can't     [modal=can,tense=pres]
ModalNeg -> couldn't  [modal=can,tense=past]
ModalNeg -> won't     [modal=will,tense=pres]
ModalNeg -> wouldn't  [modal=will,tense=past]
ModalNeg -> shan't    [modal=will,tense=pres]
ModalNeg -> mustn't   [modal=must]
ModalNeg -> mayn't    [modal=can,tense=pres]
ModalNeg -> mightn't  [modal=can,tense=pres]
ModalNeg -> shouldn't [modal=must]

StBe    -> going to StModal : StModal   [modal=will,mode=0]
StBe    -> Ving {1}      : Ving      	[pass=0,mode=prog]
StBe    -> being Ven     : Ven Pass  	[pass=1,mode=prog]
StBe    -> Ven           : Ven Pass  	[pass=1,mode=simp]
StBe    -> Pred          : Pred      	[mode=simp]
StBe    -> being Pred    : Pred      	[mode=prog]
StHave  -> Ven       {1} : Ven       	[pass=0,mode=perf]
StHave  -> been Ving {1} : Ving	    	[pass=0,mode=prog]
StHave  -> been Pred     : Pred      	[mode=perf]
StHave  -> been Ven      : Ven Pass 	[pass=1,mode=perf]
StDo    -> V             : V         	[pass=0,mode=simp]
StModal -> be Ving       : Ving -Hyor ol 
StModal -> be Pred       : Pred ol
StModal -> be Ven        : Ven Pass 
StModal -> have StHave   : StHave -mHþ ol 
StModal -> V             : V 		[pass=0,mode=simp]

Not -> not [neg=1]
Not ->     [neg=0]

V    -> sleep    : uyu
Vs   -> sleeps   : uyu
Ving -> sleeping : uyu
Ved  -> slept    : uyu
Ven  -> slept    : uyu

V    -> go       : git
Vs   -> goes     : git
Ving -> going    : git
Ved  -> went     : git
Ven  -> gone     : git

V    -> watch    : seyret
Vs   -> watches  : seyret
Ving -> watching : seyret
Ved  -> watched  : seyret
Ven  -> watched  : seyret

Tense -> : ProgTense 	[mode=prog]
Tense -> : SimpTense 	[mode=simp]
Tense -> : PerfTense 	[mode=perf]
Tense -> : ModalTense 	[mode=0]

ProgTense -> : Neg -Hyor Pers1	[tense=pres]
ProgTense -> : Neg -Hyordu Pers2    [tense=past]

SimpTense -> : -Ar Pers1	[tense=pres,neg=0]
SimpTense -> : -mA Pers3	[tense=pres,neg=1]
SimpTense -> : Neg -dH Pers2	[tense=past]

Pass -> : -Hl

PerfTense -> : Neg -dH Pers2 	[tense=pres]
PerfTense -> : Neg -mHþtH Pers2	[tense=past]

ModalTense -> : Neg -YAcAk Pers1	[modal=will,tense=pres]
ModalTense -> : Neg -YAcAktH Pers2	[modal=will,tense=past]
ModalTense -> : -Abilir Pers1		[modal=can,tense=pres,neg=0]
ModalTense -> : -Abilirdi Pers2		[modal=can,tense=past,neg=0]
ModalTense -> : -AmA Pers3		[modal=can,tense=pres,neg=1]
ModalTense -> : -AmAzdH Pers2		[modal=can,tense=past,neg=1]
ModalTense -> : -mAlH Pers1		[modal=must]

Pers1 -> : -YHm     [numb=sing,pers=1]
Pers1 -> : -sHn     [numb=sing,pers=2]
Pers1 -> :          [numb=sing,pers=3]
Pers1 -> : -YHz     [numb=plur,pers=1]
Pers1 -> : -sHnHz   [numb=plur,pers=2]
Pers1 -> : -lAr     [numb=plur,pers=3]

Pers2 -> : -m       [numb=sing,pers=1]
Pers2 -> : -n       [numb=sing,pers=2]
Pers2 -> :          [numb=sing,pers=3]
Pers2 -> : -k       [numb=plur,pers=1]
Pers2 -> : -nHz     [numb=plur,pers=2]
Pers2 -> : -lAr     [numb=plur,pers=3]

Pers3 -> : -m       [numb=sing,pers=1]
Pers3 -> : -zSHn    [numb=sing,pers=2]
Pers3 -> : -z       [numb=sing,pers=3]
Pers3 -> : -yHz     [numb=plur,pers=1]
Pers3 -> : -zsHnHz  [numb=plur,pers=2]
Pers3 -> : -zlAr    [numb=plur,pers=3]

Pred -> Adj : Adj

Adj -> happy  : mutlu

Subj -> NSing : NSing 		[numb=sing,pers=3]
Subj -> NPlur : NPlur -lAr	[numb=plur,pers=3]
Subj -> Pron  : Pron
Subj -> Pron   

Pron -> i    : ben     [numb=sing,pers=1]
Pron -> you  : sen     [numb=sing,pers=2]
Pron -> he   : o       [numb=sing,pers=3]
Pron -> she  : o       [numb=sing,pers=3]
Pron -> it   : o       [numb=sing,pers=3]
Pron -> we   : biz     [numb=plur,pers=1]
Pron -> you  : siz     [numb=plur,pers=2]
Pron -> they : onlar   [numb=plur,pers=3]

NSing -> man : adam
NPlur -> men : adam